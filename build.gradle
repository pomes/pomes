buildscript {
    repositories {
        jcenter()
        mavenLocal()
    }
    dependencies {
        classpath 'com.github.pomes.gradle-plugins:distextra:0.0.1-SNAPSHOT'
        classpath 'com.github.pomes.gradle-plugins:releaseme:0.0.1-SNAPSHOT'
    }
}

plugins {
    id 'idea'
    id 'eclipse'
    id 'com.gradle.build-scan' version '1.0'
    id 'com.github.pomes.props' version '0.1.1'
}

apply plugin: 'com.github.pomes.releaseme'

wrapper.gradleVersion = gradleVersion

propertyFiles {
    secrets {}
}

allprojects {
    repositories {
        jcenter()
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory/'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = project.propertyFiles.secrets.properties.bintrayUser
            password = project.propertyFiles.secrets.properties.bintrayKey
        }
    }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'build-dashboard'
    apply plugin: 'project-report'
    apply plugin: 'jacoco'
    apply from: "$rootProject.projectDir/gradle/codenarc.gradle"
    apply plugin: 'com.github.pomes.distextra'

    sourceCompatibility = 1.8

    dependencies {
        compile "org.codehaus.groovy:groovy-all:$groovyVersion"
        testCompile "org.spockframework:spock-core:$spockVersion"
    }
}

project(':pomes-core') {
    description = 'Core library for Pomes'

    dependencies {
        compile "org.eclipse.aether:aether-api:$aetherVersion"
        compile "org.eclipse.aether:aether-util:$aetherVersion"
        compile "org.eclipse.aether:aether-impl:$aetherVersion"
        compile "org.eclipse.aether:aether-connector-basic:$aetherVersion"
        compile "org.eclipse.aether:aether-transport-file:$aetherVersion"
        compile "org.eclipse.aether:aether-transport-http:$aetherVersion"
        compile "org.eclipse.aether:aether-transport-wagon:$aetherVersion"
        compile 'org.apache.maven.wagon:wagon-ssh:1.0'
        compile "org.apache.maven:maven-aether-provider:$mavenVersion"
        compile "org.apache.maven:maven:$mavenVersion"
        compile 'org.apache.maven:maven-project:2.2.1'

        compile 'org.glassfish.jersey.core:jersey-client:2.22.1'
        compile 'org.jsoup:jsoup:1.9.1'
        compile dependencyLogging

        testCompile 'co.freeside:betamax:1.1.2'
    }
}

project(':pomes-util') {
    description = 'Utility library for Pomes'
}

project(':pomes-cli') {
    description = 'The command line tool for Pomes'

    apply plugin: 'application'
    mainClassName = 'com.github.pomes.cli.App'

    dependencies {
        compile project(':pomes-core')
        compile 'com.beust:jcommander:1.48'
        compile dependencyLogging
        compile 'org.yaml:snakeyaml:1.17'
    }
}

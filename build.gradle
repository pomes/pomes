buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:4.0.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7'
    }
}

plugins {
    id 'idea'
    id 'eclipse'
    id 'com.gradle.build-scan' version '1.0'
}

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
}

ext {
    secrets = getSecrets()
    groovyProjects = ['pomes-core', 'pomes-cli', 'pomes-util']
}

wrapper.gradleVersion = gradleVersion

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory/'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = rootProject.ext.secrets.bintrayUser
            password = rootProject.ext.secrets.bintrayKey
        }
    }
}

bintray {
    user = rootProject.ext.secrets.bintrayUser
    key = rootProject.ext.secrets.bintrayKey
    pkg {
        repo = 'pomes'
        userOrg = 'pomes'
        labels = ['maven', 'aether', 'groovy']
    }
}

apply from: "$rootProject.projectDir/gradle/groovy-projects.gradle"

Properties getSecrets() {
    def props = new Properties()

    file("${rootDir.absolutePath}/secrets.properties").withInputStream { stream ->
        props.load(stream)
    }
    return props
}

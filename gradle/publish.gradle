configure(subprojects.findAll { it.name in groovyProjects }) {

    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.jfrog.artifactory'

    ext {
        basePom = new NodeBuilder().pom {
            name project.name
            description project.description
            url 'https://github.com/pomes/pomes'
            inceptionYear '2016'
            scm {
                url 'https://github.com/pomes/pomes.git'
                connection 'scm:git:https://github.com/pomes/pomes.git'
                developerConnection 'scm:git:https://github.com/pomes/pomes.git'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/pomes/pomes/issues'
            }
            ciManagement {
                system 'TravisCI'
                url 'https://travis-ci.org/pomes/pomes'
            }
            developers {
                developer {
                    id 'dedickinson'
                    name 'Duncan Dickinson'
                    email 'duncan@dickinson.name'
                    url 'http://www.duncan.dickinson.name'
                }
            }
        }
    }

    publishing {
        publications {
            jcentre(MavenPublication) {
                artifact tasks.groovydocJar
                artifact tasks.sourcesJar
                pom.withXml {
                    ext.basePom.each { n ->
                        asNode().append n
                    }
                }
            }
        }
    }

    artifactory {
        contextUrl = 'https://oss.jfrog.org/artifactory/'
        publish {
            repository {
                repoKey = 'oss-snapshot-local'
                username = rootProject.ext.secrets.bintrayUser
                password = rootProject.ext.secrets.bintrayKey
            }
            defaults {
                publications('jcentre')
            }
        }
    }

    bintray {
        user = rootProject.ext.secrets.bintrayUser
        key = rootProject.ext.secrets.bintrayKey
        pkg {
            repo = 'pomes'
            userOrg = 'pomes'
            issueTrackerUrl = project.ext.basePom.issueManagement[0].url[0].value()
            labels = ['maven', 'aether', 'groovy']
        }
    }
}
